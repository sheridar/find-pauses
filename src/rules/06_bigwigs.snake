# ====== Rules for creating bigwigs files from aligned reads ===================


# Create bigwigs using single bp read coordinates 
# Do not reverse strands since read #1 from QIA-seq libraries is the sense
# direction
rule bigwigs:
    input:
        RESULTS + "/{sam_grp}/{sam_grp}.bed.gz"
    output:
        p_bg   = temp_fn(RESULTS + "/{sam_grp}/{sam_grp}_pos.bedgraph.gz"),
        p_bg_N = temp_fn(RESULTS + "/{sam_grp}/{sam_grp}_pos_N.bedgraph.gz"),
        n_bg   = temp_fn(RESULTS + "/{sam_grp}/{sam_grp}_neg.bedgraph.gz"),
        n_bg_N = temp_fn(RESULTS + "/{sam_grp}/{sam_grp}_neg_N.bedgraph.gz"),

        p_bw   = RESULTS + "/{sam_grp}/{sam_grp}_pos.bw",
        n_bw   = RESULTS + "/{sam_grp}/{sam_grp}_neg.bw"
    params:
        job_name = "{sam_grp}_bigwig",
        memory   = MEMORY,
        chroms   = CHROMS
    log:
        out = RESULTS + "/logs/{sam_grp}_bigwig.out",
        err = RESULTS + "/logs/{sam_grp}_bigwig.err"
    benchmark:
        RESULTS + "/benchmarks/{sam_grp}_bigwig.tsv"
    message:
        "Creating bigwigs for {wildcards.sam_grp}"
    threads:
        6
    shell:
        """
        source {SRC}/funs.sh
        
        # Create bigwigs for each strand
        # Normalized by the number of reads in the starting bed file (RPM)
        create_bigwig \
            {input} \
            "-strand +" \
            1 \
            {output.p_bg} \
            {output.p_bg_N} \
            {output.p_bw} \
            {params.chroms} \
            {threads}

        create_bigwig \
            {input} \
            "-strand -" \
            -1 \
            {output.n_bg} \
            {output.n_bg_N} \
            {output.n_bw} \
            {params.chroms} \
            {threads}
        """


# Create IGV browser session
rule igv_browser:
    input:
        pos = expand(
            RESULTS + "/{sam_grp}/{sam_grp}_pos.bw",
            sam_grp = SAM_GRPS
        ),
        neg = expand(
            RESULTS + "/{sam_grp}/{sam_grp}_neg.bw",
            sam_grp = SAM_GRPS
        )
    output:
        urls = RESULTS + "/urls/" + PROJ + "_urls.tsv",
        xml  = RESULTS + "/urls/" + PROJ + "_igv.xml"
    params:
        job_name = PROJ + "_igv",
        memory   = 4,
        url      = URL,
        ssh      = SSH
    log:
        out = RESULTS + "/logs/" + PROJ + "_igv.out",
        err = RESULTS + "/logs/" + PROJ + "_igv.err"
    benchmark:
        RESULTS + "/benchmarks/" + PROJ + "_igv.tsv"
    message:
        "Creating " + PROJ + " IGV session"
    threads:
        1
    shell:
        """
        source {SRC}/funs.sh

        if [ ! -d "$HOME/.ssh" ] && [ ! -L "$HOME/.ssh" ]
        then
            ln -s "{SSH_KEY}" "$HOME"
        fi

        create_urls \
            "{input.pos}" \
            {params.ssh} \
            {params.url} \
            {output.urls} \
            {GRP_REGEX}

        create_urls \
            "{input.neg}" \
            {params.ssh} \
            {params.url} \
            {output.urls} \
            {GRP_REGEX}

        # Create IGV session
        {SRC}/write_igv_xml.py \
            {output.urls} \
            {output.xml}
        """

 
