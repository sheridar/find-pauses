# ===== Rules for running FastQC ===============================================


# Run fastqc
rule fastqc:
    input:
        FASTQ_DIR + "/{fastq}"
    output:
        zip  = RESULTS + "/fastqc/{fastq}_fastqc.zip",
        html = RESULTS + "/fastqc/{fastq}_fastqc.html"
    params:
        job_name = "{fastq}_fastqc",
        out      = RESULTS + "/fastqc"
    resources:
        mem_mb = _get_mem(2, 1000),
        mem_gb = _get_mem(2)
    log:
        out = RESULTS + "/logs/{fastq}_fastqc.out",
        err = RESULTS + "/logs/{fastq}_fastqc.err"
    threads:
        6
    shell:
        """
        mkdir -p '{params.out}'

        fastqc \
            -t {threads} \
            -f fastq \
            -o '{params.out}' \
            '{input}'

        nm='{wildcards.fastq}'
        nm="${{nm%.*}}"
        nm="${{nm%.*}}"

        mv "{params.out}/${{nm}}_fastqc.zip"  '{output.zip}'
        mv "{params.out}/${{nm}}_fastqc.html" '{output.html}'
        """


# Combine FastQC summaries
rule fastqc_summary:
    input:
        expand(
            RESULTS + "/fastqc/{fastq}_fastqc.zip",
            fastq = FASTQS
        )
    output:
        RESULTS + "/stats/" + PROJ + "_fastqc.tsv"
    params:
        job_name = PROJ + "_fastqc_summary",
    resources:
        mem_mb = _get_mem(0.125, 1000),
        mem_gb = _get_mem(0.125)
    log:
        out = RESULTS + "/logs/" + PROJ + "_fastqc_summary.out",
        err = RESULTS + "/logs/" + PROJ + "_fastqc_summary.err"
    threads:
        1
    shell:
        """
        for dir in {input};
        do
            nm=$(basename -s .zip "$dir")
            nm="${{nm%.*}}"
            nm="${{nm%.*}}_fastqc"

            unzip -p "$dir" "$nm/summary.txt" \
                >> '{output}'
        done
        """


