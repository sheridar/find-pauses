# ====== Rules for creating bigwigs files from aligned reads ===================


# Create bigwigs using single bp read coordinates 
# Do not reverse strands since read #1 from QIA-seq libraries is the sense
# direction
rule bigwigs_nosub:
    input:
        RESULTS + "/{sample}/{sample}_shift.bed.gz"
    output:
        p_bg   = temp_fn(RESULTS + "/{sample}/{sample}_pos.bedgraph.gz"),
        p_bg_N = temp_fn(RESULTS + "/{sample}/{sample}_pos_N.bedgraph.gz"),
        n_bg   = temp_fn(RESULTS + "/{sample}/{sample}_neg.bedgraph.gz"),
        n_bg_N = temp_fn(RESULTS + "/{sample}/{sample}_neg_N.bedgraph.gz"),

        p_bw   = RESULTS + "/{sample}/{sample}_pos.bw",
        n_bw   = RESULTS + "/{sample}/{sample}_neg.bw"
    params:
        job_name = "{sample}_bigwig",
        chroms   = CHROMS
    resources:
        mem_gb = _get_mem(0.5)
    log:
        out = RESULTS + "/logs/{sample}_bigwig.out",
        err = RESULTS + "/logs/{sample}_bigwig.err"
    benchmark:
        RESULTS + "/benchmarks/{sample}_bigwig.tsv"
    threads:
        6
    shell:
        """
        source '{SRC}/funs.sh'
        
        # Create bigwigs for each strand
        # Normalized by the number of reads in the starting bed file (RPM)
        create_bigwig \
            '{input}' \
            '-strand +' \
            1 \
            '{output.p_bg}' \
            '{output.p_bg_N}' \
            '{output.p_bw}' \
            '{params.chroms}' \
            {threads}

        create_bigwig \
            '{input}' \
            '-strand -' \
            -1 \
            '{output.n_bg}' \
            '{output.n_bg_N}' \
            '{output.n_bw}' \
            '{params.chroms}' \
            {threads}
        """


# Create IGV browser session
rule igv_browser_nosub:
    input:
        pos = expand(
            RESULTS + "/{sample}/{sample}_pos.bw",
            sample = SAMS_UNIQ
        ),
        neg = expand(
            RESULTS + "/{sample}/{sample}_neg.bw",
            sample = SAMS_UNIQ
        )
    output:
        urls = RESULTS + "/urls/" + PROJ + "_nosub_urls.tsv",
        xml  = RESULTS + "/urls/" + PROJ + "_nosub_igv.xml"
    params:
        job_name    = PROJ + "_nosub_igv",
        url         = URL,
        ssh         = SSH,
        ssh_key_dir = SSH_KEY_DIR
    resources:
        mem_gb = _get_mem(0.25)
    log:
        out = RESULTS + "/logs/" + PROJ + "_nosub_igv.out",
        err = RESULTS + "/logs/" + PROJ + "_nosub_igv.err"
    benchmark:
        RESULTS + "/benchmarks/" + PROJ + "_nosub_igv.tsv"
    threads:
        1
    shell:
        """
        source '{SRC}/funs.sh'

        create_urls \
            '{input.pos}' \
            '{params.ssh}' \
            '{params.ssh_key_dir}' \
            '{params.url}' \
            '{output.urls}'

        create_urls \
            '{input.neg}' \
            '{params.ssh}' \
            '{params.ssh_key_dir}' \
            '{params.url}' \
            '{output.urls}'

        # Create IGV session
        '{SRC}/write_igv_xml.py' \
            '{output.urls}' \
            '{output.xml}'
        """

 
# Create bigwigs using reads that have not been filtered based on
# proximity to protein coding genes and have not been subsampled
rule bigwigs_nofilt:
    input:
        RESULTS + "/{sample}/{sample}_nofilt.bed.gz"
    output:
        p_bg   = temp_fn(RESULTS + "/{sample}/{sample}_nofilt_pos.bedgraph.gz"),
        p_bg_N = temp_fn(RESULTS + "/{sample}/{sample}_nofilt_pos_N.bedgraph.gz"),
        n_bg   = temp_fn(RESULTS + "/{sample}/{sample}_nofilt_neg.bedgraph.gz"),
        n_bg_N = temp_fn(RESULTS + "/{sample}/{sample}_nofilt_neg_N.bedgraph.gz"),

        p_bw   = RESULTS + "/{sample}/{sample}_nofilt_pos.bw",
        n_bw   = RESULTS + "/{sample}/{sample}_nofilt_neg.bw"
    params:
        job_name = "{sample}_nofilt_bigwig",
        chroms   = CHROMS
    resources:
        mem_gb = _get_mem(0.5)
    log:
        out = RESULTS + "/logs/{sample}_nofilt_bigwig.out",
        err = RESULTS + "/logs/{sample}_nofilt_bigwig.err"
    benchmark:
        RESULTS + "/benchmarks/{sample}_nofilt_bigwig.tsv"
    threads:
        6
    shell:
        """
        source '{SRC}/funs.sh'

        # Create bigwigs for each strand
        # Normalized by the number of reads in the starting bed file (RPM)
        create_bigwig \
            '{input}' \
            '-strand +' \
            1 \
            '{output.p_bg}' \
            '{output.p_bg_N}' \
            '{output.p_bw}' \
            '{params.chroms}' \
            {threads}

        create_bigwig \
            '{input}' \
            '-strand -' \
            -1 \
            '{output.n_bg}' \
            '{output.n_bg_N}' \
            '{output.n_bw}' \
            '{params.chroms}' \
            {threads}
        """


# Create IGV browser session
rule igv_browser_nofilt:
    input:
        pos = expand(
            RESULTS + "/{sample}/{sample}_nofilt_pos.bw",
            sample = SAMS_UNIQ
        ),
        neg = expand(
            RESULTS + "/{sample}/{sample}_nofilt_neg.bw",
            sample = SAMS_UNIQ
        )
    output:
        urls = RESULTS + "/urls/" + PROJ + "_nofilt_urls.tsv",
        xml  = RESULTS + "/urls/" + PROJ + "_nofilt_igv.xml"
    params:
        job_name    = PROJ + "_nofilt_igv",
        url         = URL,
        ssh         = SSH,
        ssh_key_dir = SSH_KEY_DIR
    resources:
        mem_gb = _get_mem(0.25)
    log:
        out = RESULTS + "/logs/" + PROJ + "nofilt_igv.out",
        err = RESULTS + "/logs/" + PROJ + "nofilt_igv.err"
    benchmark:
        RESULTS + "/benchmarks/" + PROJ + "nofilt_igv.tsv"
    threads:
        1
    shell:
        """
        source '{SRC}/funs.sh'

        create_urls \
            '{input.pos}' \
            '{params.ssh}' \
            '{params.ssh_key_dir}' \
            '{params.url}' \
            '{output.urls}'

        create_urls \
            '{input.neg}' \
            '{params.ssh}' \
            '{params.ssh_key_dir}' \
            '{params.url}' \
            '{output.urls}'

        # Create IGV session
        '{SRC}/write_igv_xml.py' \
            '{output.urls}' \
            '{output.xml}'
        """


